_lineComment : '#' {.} '\n' ;
!comment : _lineComment;

!whitespace: ' ' | '\t' | '\n' | '\r' ;
_letter : 'a'-'z' | 'A'-'Z' ;

_digit : '0'-'9' ;

_idchar : _letter | _digit | '_' | '-' | '.' | '/';

_sizecode : 'k' | 'm' | 'g' | 't' | 'p' | '%';

_uint : _digit {_digit};

_anything	: 'A'-'Z' | 'a'-'z' | '0'-'9'
		| '!' | '#' | '$' | '%' | '&' | '\'' | '*' | '+' | '-' | '/'
		| '=' | '?' | '^' | '_' | '`' | '{' | '|' | '}' | '~' | '.' | '@'
		| '\u0100'-'\U0010FFFF';
_atom : _anything {_anything} ;
_quotedpair : '\\' . ;
_quotedstring : '"' (_quotedpair | .) {_quotedpair | .} '"' ;
name : (_atom | _quotedstring) ;

<< import "github.com/mperham/inspeqtor/jobs/ast" >>

/*

Examples:

check jobs
  temp_cleanup happens every 5 minutes
  verify-deposits happens every hour
  process-credit-batches happens every day

*/

Check
  : Preamble Checktype ParameterList JobList  << ast.NewJobCheck($3, $2), nil >>
  | Preamble Checktype JobList                << ast.NewJobCheck($2, map[string]string{}), nil >>
;

Preamble
 : "check"          << $0, nil >>
;

Checktype
 : "jobs"        << $0, nil >>
;

ParameterList
  : "with" Parameters   << $1, nil >>;

Parameters
  : name name "," Parameters      << ast.AddParam($0, $1, $3) >>
  | name name                     << ast.AddParam($0, $1, nil) >>;

IntAmount
  : name            << ast.ToInt64($0) >>;

Period
  : "hour"    << $0, nil >>
  | "hours"   << $0, nil >>
  | "minute"  << $0, nil >>
  | "minutes" << $0, nil >>
  | "day"     << $0, nil >>
  | "days"    << $0, nil >>;

Job
  : name "happens" "every" IntAmount Period  << ast.NewJob($0, $3, $4), nil >>
  | name "happens" "every" Period            << ast.NewJob($0, int64(1), $3), nil >>;

JobList
  : Job                              << ast.NewJobList($0), nil >>
  | JobList Job                      << ast.AppendJob($0, $1), nil >>;
