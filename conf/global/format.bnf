// https://github.com/awalterschulze/katydid/blob/master/asm/asm.bnf

/*
Global parser for /etc/inspeqtor/inspeqtor.conf
*/

_lineComment : '#' {.} '\n' ;
!comment : _lineComment;

!whitespace: ' ' | '\t' | '\n' | '\r' ;

_anything	: 'A'-'Z' | 'a'-'z' | '0'-'9'
		| '!' | '#' | '$' | '%' | '&' | '\'' | '*' | '+' | '-' | '/'
		| '=' | '?' | '^' | '_' | '`' | '{' | '|' | '}' | '~' | '@' | '.' | ':' | ';' | '<' | '>'
		| '\u0100'-'\U0010FFFF';
_atom : _anything {_anything} ;
_quotedpair : '\\' . ;
_quotedstring : '"' (_quotedpair | .) {_quotedpair | .} '"' ;
value : (_atom | _quotedstring) ;

<< import "inspeqtor/conf/global/ast" >>

Config
  : SetStatement                  << ast.NewConfig($0.(map[string]string), nil) >>
  | RouteStatement                << ast.NewConfig(map[string]string{}, $0) >>
  | SetStatement Config           << ast.AddSet($0, $1) >>
  | RouteStatement Config         << ast.AddRoute($0, $1) >>
;

RouteStatement
  : "send" "alerts" "via" value "with" ChannelParameters            << ast.NewRoute(nil, $3, $5) >>
  | "send" "alerts" "to" value "via" value "with" ChannelParameters << ast.NewRoute($3, $5, $7) >>
;

ChannelParameters
  : value value                          << ast.AppendPair($0, $1, map[string]string{}) >>
  | value value "," ChannelParameters    << ast.AppendPair($0, $1, $3) >>
;

SetStatement
  : "set" value value            << ast.AppendPair($1, $2, map[string]string{}) >>
;
